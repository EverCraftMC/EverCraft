buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.hierynomus:sshj:0.34.0"
    }
}

plugins {
    id "java"
}

sourceCompatibility = JavaVersion.toVersion(project.java_version)
targetCompatibility = JavaVersion.toVersion(project.java_version)

archivesBaseName = "evercraft"
group = "io.github.evercraftmc"
version = project.project_version

repositories {
    mavenCentral()
}

subprojects {
    group = "io.github.evercraftmc.evercraft"

    configurations {
        dependency
        dependency.canBeResolved = true

        compileOnly {
            extendsFrom dependency
        }
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.toVersion(project.java_version)
        targetCompatibility = JavaVersion.toVersion(project.java_version)

        options.encoding = "UTF-8"
    }
}

tasks.register("buildAll") {
    dependsOn subprojects.collect { it.tasks.jar }
}

import net.schmizz.sshj.SSHClient
import net.schmizz.sshj.sftp.SFTPClient

tasks.register("upload") {
    dependsOn tasks.buildAll

    doLast {
        def servers = System.getenv("EVERCRAFT_SERVERS").split(";")

        servers.each() {
            def server = it

            if ((System.getProperty("type") == null || System.getProperty("type").equalsIgnoreCase(server.split(":")[1])) && findProject(":" + server.split(":")[1]) != null) {
                SSHClient ssh = new SSHClient()
                ssh.loadKnownHosts()
                ssh.connect(System.getenv("EVERCRAFT_FTP_HOST").split(":")[0], Integer.parseInt(System.getenv("EVERCRAFT_FTP_HOST").split(":")[1]))
                ssh.authPassword(System.getenv("EVERCRAFT_FTP_USERNAME") + "." + server.split(":")[0], System.getenv("EVERCRAFT_FTP_PASSWORD"))

                SFTPClient ftp = ssh.newSFTPClient()

                ftp.ls(server.split(":")[2]).each() {
                    if (it.getName().startsWith("EverCraft-") && it.getName().endsWith(".jar") && it.isRegularFile()) {
                        ftp.rm(it.getPath())
                    }
                }

                ftp.put(project(":" + server.split(":")[1]).tasks.jar.outputs.files[0].getPath(), server.split(":")[2] + "/EverCraft-" + project(":" + server.split(":")[1]).tasks.jar.outputs.files[0].getName())

                ftp.close()
                ssh.disconnect()
            }
        }
    }
}