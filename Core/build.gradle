plugins {
    id "java"
}

version = core_version

repositories {
    maven {
        url "https://maven.kaleko.dev/public-snapshot/"
    }
    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }
    mavenCentral()
}

dependencies {
    implementation "io.github.kale_ko:ejcl:${ejcl_version}"

    compileOnly "io.github.waterfallmc:waterfall-api:${waterfall_version}"
    compileOnly "io.papermc.paper:paper-api:${paper_version}"

    compileOnly "org.slf4j:slf4j-api:2.0.9"
}

tasks.register("fatJar", Jar) {
    archiveClassifier = "fat"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.output)
    from(configurations.runtimeClasspath.collect {
        it.isDirectory() ? null : zipTree(it)
    })
}

build {
    finalizedBy tasks.fatJar
}

processResources {
    inputs.property "version", core_version

    filesMatching(["bungee.yml", "plugin.yml"]) {
        expand "version": core_version
    }
}